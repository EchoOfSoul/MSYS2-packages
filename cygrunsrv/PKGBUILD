# Maintainer: Martell Malone <martellmalone@gmail.com>

pkgname=cygrunsrv
pkgver=1.62
pkgrel=1
pkgdesc="${pkgname} is a windows services tool"
_realname=cygrunsrv
arch=('i686' 'x86_64')
url="https://cygwin.org/"
license=('GPL')
depends=('python2')
makedepends=('gcc' 'git' 'make')
source=(${pkgname}-${pkgver}::git://sourceware.org/git/cygwin-apps/${pkgname}.git#tag=rel${pkgver//./-}
        msysize.patch)
sha256sums=(SKIP
            'd3c9f9dcc081d40d7624deed53ad29d0b4d90eb0d873f92a5519b5a0bef90ee6')

prepare(){
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/msysize.patch

  autoreconf -fi
}

build() {

  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"

  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST}

  make
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
}
