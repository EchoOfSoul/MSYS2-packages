# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mercurial
pkgver=3.7.3
pkgrel=1
pkgdesc="A scalable distributed SCM tool"
arch=('i686' 'x86_64')
url="https://www.mercurial-scm.org/"
license=('GPL')
groups=('VCS')
depends=('python2')
#optdepends=('tk: for the hgk GUI')
backup=('etc/mercurial/hgrc')
source=("https://mercurial-scm.org/release/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'mercurial.profile'
        'mercurial-2.6.1-msys2.patch')
sha256sums=('c099c42d74e2d520b61dd372cd996b0fa7605c06617834fd7b13c79b9a9a5b30'
            'SKIP'
            '87427151713e689cd87dc50d50c048e0e58285815e4eb61962b50583532cbde5'
            'ea2bcabcd85968c23b1930de379d6c620a4a669c4419297238d94b31682f9fd2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/mercurial-2.6.1-msys2.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  sed -i -e 's#env python#env python2#' \
    "${pkgdir}"/usr/lib/python2.7/site-packages/mercurial/lsprof.py

  install -d ${pkgdir}/usr/share/man/{man1,man5}
  install -m644 doc/hg.1 "${pkgdir}/usr/share/man/man1"
  install -m644 doc/{hgrc.5,hgignore.5} "${pkgdir}/usr/share/man/man5"
  install -m755 contrib/hgk "${pkgdir}/usr/bin"
  #install -m644 -D contrib/zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_hg"
  install -m644 -D contrib/bash_completion "${pkgdir}/usr/share/bash-completion/completions/hg"
  install -d "${pkgdir}/usr/share/emacs/site-lisp"
  install -m644 contrib/{mq.el,mercurial.el} "${pkgdir}/usr/share/emacs/site-lisp"

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm644 contrib/vim/HGAnnotate.vim "${vimpath}/syntax/HGAnnotate.vim"

  # set some variables
  install -m755 -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/mercurial.profile "${pkgdir}/etc/profile.d/mercurial.sh"

  # install configuration file
  install -m755 -d ${pkgdir}/etc/mercurial
  #install -m644 contrib/sample.hgrc "${pkgdir}/etc/mercurial/hgrc"
  
  # FS#38825 - Add certs config to package
  echo -e "\n[web]\ncacerts = /usr/ssl/certs/ca-bundle.crt\n" >> "${pkgdir}/etc/mercurial/hgrc"
}
